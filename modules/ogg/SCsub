#!/usr/bin/env python

Import("env")
Import("env_modules")

env_ogg = env_modules.Clone()

# Thirdparty source files

thirdparty_obj = []

if env["builtin_libogg"]:
    thirdparty_dir = "#thirdparty/libogg/"
    thirdparty_sources = [
        "bitwise.c",
        "framing.c",
    ]
    thirdparty_sources = [thirdparty_dir + file for file in thirdparty_sources]

    env_ogg.Prepend(CPPPATH=[thirdparty_dir])

    env_thirdparty = env_ogg.Clone()
    env_thirdparty.disable_warnings()
    env_thirdparty.add_source_files(thirdparty_obj, thirdparty_sources)
    env.modules_sources += thirdparty_obj
else:
    ogg_thirdparty_dir = "#thirdparty-precompiled/libogg_x64-windows-static/"
    env_ogg.Append(CPPPATH=[ogg_thirdparty_dir + "include"])
    env.Append(LIBPATH=[ogg_thirdparty_dir + "lib"])
    env.Append(LINKFLAGS= [
       "ogg.lib",
    ])

# Godot source files

module_obj = []

env_ogg.add_source_files(module_obj, "*.cpp")
env.modules_sources += module_obj

# Needed to force rebuilding the module files when the thirdparty library is updated.
env.Depends(module_obj, thirdparty_obj)
